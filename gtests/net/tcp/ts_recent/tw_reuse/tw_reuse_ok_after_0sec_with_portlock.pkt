// Check that TIME-WAIT 4-tuple can be reused immediately if bind() was used to
// reserve the source port.

--bind_port=57005
--tcp_ts_tick_usecs=1000
--tolerance_percent=1

`
../../common/defaults.sh
../../../tcp/common/set_sysctls.py  /proc/sys/net/ipv4/tcp_tw_reuse=1 # global enable
../../../tcp/common/set_sysctls.py  /proc/sys/net/ipv4/tcp_tw_reuse_delay=1000 # msec
`

//
// Prime TIME-WAIT - open first connection and close it.
// Use source port 57005. Reserve it with bind().
//

    0   socket(..., SOCK_STREAM | SOCK_NONBLOCK, IPPROTO_TCP) = 3

   +0   setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0   bind(3, ..., ...) = 0

   +0   connect(3, ..., ...) = -1 EINPROGRESS (Operation now in progress)

   +0   > S  0:0(0)                 <mss 1460,sackOK,TS val 1001 ecr 0,nop,wscale 8>
+.001   < S. 0:0(0) ack 1 win 65535 <mss 1460,sackOK,TS val 3001 ecr 1001,nop,wscale 8>
   +0   > .  1:1(0) ack 1           <nop,nop,TS val 1002 ecr 3001>

   +0   close(3) = 0

   +0   > F. 1:1(0) ack 1       <nop,nop,TS val 1002 ecr 3001>
+.001   < F. 1:1(0) ack 2 win 8 <nop,nop,TS val 3002 ecr 1002>
   +0   > .  2:2(0) ack 2       <nop,nop,TS val 1003 ecr 3002>

//
// Reincarnation without delay - open and close second connection.
// Use same source port.
//

   +0   socket(..., SOCK_STREAM | SOCK_NONBLOCK, IPPROTO_TCP) = 3

   +0   setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0   bind(3, ..., ...) = 0

+0.00   connect(3, ..., ...) = -1 EINPROGRESS (Operation now in progress)

// NOTE: Packetdrill doesn't actually track timestamps across connection
// reincarnations despite the fact that the random offset doesn't change.
   +0   > S  0:0(0)                 <mss 1460,sackOK,TS val 1003 ecr 0,nop,wscale 8>
+.001   < S. 0:0(0) ack 1 win 65535 <mss 1460,sackOK,TS val 3002 ecr 1003,nop,wscale 8>
   +0   > .  1:1(0) ack 1           <nop,nop,TS val 1004 ecr 3002>

   +0   close(3) = 0

   +0   > F. 1:1(0) ack 1       <nop,nop,TS val 1004 ecr 3002>
+.001   < F. 1:1(0) ack 2 win 8 <nop,nop,TS val 3003 ecr 1004>
   +0   > .  2:2(0) ack 2       <nop,nop,TS val 1005 ecr 3003>
